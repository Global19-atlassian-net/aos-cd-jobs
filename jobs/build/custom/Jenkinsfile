node {
    checkout scm
    def commonlib = load("pipeline-scripts/commonlib.groovy")

    // Expose properties for a parameterized build
    properties(
        [
            buildDiscarder(
                logRotator(
                    artifactDaysToKeepStr: '',
                    artifactNumToKeepStr: '',
                    daysToKeepStr: '',
                    numToKeepStr: '1000')),
            [
                $class: 'ParametersDefinitionProperty',
                parameterDefinitions: [
                    commonlib.oseVersionParam('BUILD_VERSION'),
                    [
                        name: 'VERSION',
                        description: 'Version string for build (e.g. 4.0.0)',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: ""
                    ],
                    [
                        name: 'RELEASE',
                        description: 'Release string for build',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: ""
                    ],
                    [
                        name: 'SKIP_OSE',
                        description: 'If certain the ose repo is not needed, save minutes by not cloning it.',
                        $class: 'hudson.model.BooleanParameterDefinition',
                        defaultValue: false
                    ],
                    [
                        name: 'RPMS',
                        description: 'CSV list of RPMs to build. Empty for all. Enter "NONE" to not build any.',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: "NONE"
                    ],
                    [
                        name: 'IMAGES',
                        description: 'CSV list of images to build. Empty for all. Enter "NONE" to not build any.',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: ""
                    ],
                    [
                        name: 'REBASE_IMAGES',
                        description: 'Run images:rebase? Otherwise use images:update-dockerfile',
                        $class: 'hudson.model.BooleanParameterDefinition',
                        defaultValue: true
                    ],
                    [
                        name: 'SIGNED',
                        description: 'Build images against signed RPMs?',
                        $class: 'hudson.model.BooleanParameterDefinition',
                        defaultValue: false
                    ],
                    [
                        name: 'MAIL_LIST_SUCCESS',
                        description: 'Success Mailing List',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: [
                            'aos-team-art@redhat.com',
                        ].join(',')
                    ],
                    [
                        name: 'MAIL_LIST_FAILURE',
                        description: 'Failure Mailing List',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: [
                            'aos-team-art@redhat.com',
                        ].join(',')
                    ],
                    commonlib.mockParam(),
                ]
            ],
        ]
    )

    def buildlib = load("pipeline-scripts/buildlib.groovy")
    buildlib.initialize(false)
    GITHUB_BASE = "git@github.com:openshift" // buildlib uses this global var

    master_ver = commonlib.ocpDefaultVersion
    version = commonlib.standardVersion(params.VERSION)
    release = params.RELEASE.trim()
    repo_type = params.SIGNED ? "signed" : "unsigned"
    images = commonlib.cleanCommaList(params.IMAGES)
    rpms = commonlib.cleanCommaList(params.RPMS)

    // doozer_working must be in WORKSPACE in order to have artifacts archived
    doozer_working = "${WORKSPACE}/doozer_working"
    //Clear out previous workspace
    sh "rm -rf ${doozer_working}"
    sh "mkdir -p ${doozer_working}"

    try {
        sshagent(["openshift-bot"]) {
            // To work on real repos, buildlib operations must run with the permissions of openshift-bot

            // Some images require OSE as a source.
            // Instead of trying to figure out which do, always clone
            stage("ose repo") {
                if (params.SKIP_OSE) { return }
                // defines:
                //   OPENSHIFT_DIR // by calling initialize_openshift_dir()
                ///  OSE_DIR
                //   GITHUB_URLS["ose"]
                //   GITHUB_BASE_PATHS["ose"]
                buildlib.initialize_openshift_dir()
                checkout_branch = "enterprise-${params.BUILD_VERSION}"
                if(params.BUILD_VERSION == master_ver){ checkout_branch = "master"}

                // since there's no merge and commit back, single depth is way faster
                dir( OPENSHIFT_DIR ) {
                    sh "git clone -b ${checkout_branch} --single-branch ${GITHUB_BASE}/ose.git --depth 1"
                    GITHUB_URLS["ose"] = "${GITHUB_BASE}/ose.git"
                }

                OSE_DIR = "${OPENSHIFT_DIR}/ose"
                GITHUB_BASE_PATHS["ose"] = OSE_DIR
                env.OSE_DIR = OSE_DIR
                echo "Initialized env.OSE_DIR: ${env.OSE_DIR}"
            }

            stage("rpm builds") {
                if (rpms.toUpperCase() != "NONE") {
                    command = "--working-dir ${doozer_working} --group 'openshift-${params.BUILD_VERSION}' "
                    if (rpms) { command += "-r '${rpms}' " }
                    if (!params.SKIP_OSE) { command += "--source ose ${OSE_DIR} " }
                    command += "rpms:build --version ${version} --release ${release} "
                    buildlib.doozer command
                }
            }

            stage("puddle: ose 'building'") {
                if (rpms.toUpperCase() != "NONE") {
                    AOS_CD_JOBS_COMMIT_SHA = sh(
                        returnStdout: true,
                        script: "git rev-parse HEAD",
                    ).trim()
                    PUDDLE_CONF_BASE = "https://raw.githubusercontent.com/openshift/aos-cd-jobs/${AOS_CD_JOBS_COMMIT_SHA}/build-scripts/puddle-conf"
                    PUDDLE_CONF = "${PUDDLE_CONF_BASE}/atomic_openshift-${params.BUILD_VERSION}.conf"
                    OCP_PUDDLE = buildlib.build_puddle(
                        PUDDLE_CONF,    // The puddle configuration file to use
                        null, // openshifthosted key
                        "-b",   // do not fail if we are missing dependencies
                        "-d",   // print debug information
                        "-n",   // do not send an email for this puddle
                        "-s",   // do not create a "latest" link since this puddle is for building images
                        "--label=building"   // create a symlink named "building" for the puddle
                    )
                }
            }

            stage("update dist-git") {
                if (images.toUpperCase() != "NONE") {
                    task = params.REBASE_IMAGES ? "rebase" : "update-dockerfile"

                    command = "--working-dir ${doozer_working} --group 'openshift-${params.BUILD_VERSION}' "
                    command += "--latest-parent-version "
                    if (!params.SKIP_OSE) { command += "--source ose ${OSE_DIR} " }
                    if (images) { command += "-i '${images}' " }
                    command += "images:${task} --version ${version} --release ${release} "
                    command += "--repo-type ${repo_type} "
                    command += "--message 'Updating Dockerfile version and release ${version}-${release}' --push "
                    buildlib.doozer command
                }
            }

            stage("build images") {
                if (images.toUpperCase() != "NONE") {
                    command = "--working-dir ${doozer_working} --group 'openshift-${params.BUILD_VERSION}' "
                    if (images) { command += "-i '${images}' " }
                    command += "images:build --push-to-defaults --repo-type ${repo_type} "
                    buildlib.doozer command
                }
            }

            mail(to: "${params.MAIL_LIST_SUCCESS}",
                from: "aos-team-art@redhat.com",
                subject: "Successful custom OCP build: ${version}-${release}",
                body: "Jenkins job: ${env.BUILD_URL}");
        }
    } catch (err) {
        mail(to: "${params.MAIL_LIST_FAILURE}",
             from: "aos-team-art@redhat.com",
             subject: "Error building custom OCP: ${version}-${release}",
             body: """Encountered an error while running OCP pipeline: ${err}

    Jenkins job: ${env.BUILD_URL}
    """);

        currentBuild.result = "FAILURE"
        throw err
    } finally {
        commonlib.safeArchiveArtifacts([
            "doozer_working/*.log",
            "doozer_working/*.yaml",
            "doozer_working/brew-logs/**",
        ])
    }
}
