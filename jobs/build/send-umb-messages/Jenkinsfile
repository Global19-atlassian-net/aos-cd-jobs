#!/usr/bin/env groovy

node {
    properties([
            buildDiscarder(
                logRotator(
                    artifactDaysToKeepStr: '',
                    artifactNumToKeepStr: '',
                    daysToKeepStr: '30',
                    numToKeepStr: ''
                )
            ),
            disableConcurrentBuilds(),
    ])

    // we only care to publish messages for the following releases
    releases = ["4-stable", "4.2.0-0.nightly", "4.1.0-0.nightly"]
    currentBuild.description = ""
    currentBuild.displayName = ""

    stage("send UMB messages for new releases") {
        dir ("/mnt/nfs/home/jenkins/.cache/releases") {
            for (String release : releases) {
                latestRelease = sh(
                    returnStdout: true,
                    script: "curl -s https://openshift-release.svc.ci.openshift.org/api/v1/releasestream/${release}/latest",
                ).trim()
		try {
                    previousRelease = readFile("${release}.current")
		} catch (readex) {
		    // The first time this job is ran and the first
		    // time any new release is added the 'readFile'
		    // won't find the file and will raise a
		    // NoSuchFileException exception.
		    touch file: "${release}.current"
		    previousRelease = ""
		}

                if ( latestRelease != previousRelease ) {
		    currentBuild.displayName += "ðŸ†•: ${release} "
		    currentBuild.description += "\nðŸ†•: ${release}"
		    def msgJson = readJSON file: '', text: latestRelease
		    def messageProperties = """name=${msgJson.name}
pullSpec=${msgJson.pullSpec}
downloadURL=${msgJson.downloadURL}
"""
                    sendCIMessage(
                        messageContent: "New release payload for OpenShift ${release}",
                        messageProperties: "${messageProperties}",
                        messageType: 'Custom',
                        overrides: [topic: 'VirtualTopic.qe.ci.jenkins'],
                        providerName: 'Red Hat UMB'
                    )
                    writeFile file: "${release}.current", text: "${latestRelease}"
                } else {
		    currentBuild.description += "\nUnchanged: ${release}"
		}
            }
        }
    }
}
